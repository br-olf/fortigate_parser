root : config+

?config : "config" config_branch subcommand* "end" _LINEEND+

?subcommand : subcommand_config
            | subcommand_field_set
            | subcommand_field_unset
            | subcommand_table_edit

subcommand_config : config

config_branch : _config_branch_specifier _LINEEND+
_config_branch_specifier: NAME+ [ESCAPED_STRING]



FIELD: NAME
VALUE_CONTENT: (CONCATENATED_PRINTABLE|ESCAPED_STRING)
TABLE: (INT|ESCAPED_STRING)

subcommand_field_set : "set" FIELD value _LINEEND+
subcommand_field_unset : "unset" FIELD _LINEEND+
subcommand_table_edit: "edit" TABLE _LINEEND+ subcommand* "next" _LINEEND+



value : VALUE_CONTENT+

NAME: /[\w\-]+/
CONCATENATED_PRINTABLE : /[\w\-\.:\/\*]+/
COMMENT : /#.*/ _LINEEND+
_LINEEND : WS_INLINE* NEWLINE
ESCAPED_STRING : /"(.|\n)*?"/ | /'(.|\n)*?'/


%import common.WS_INLINE
%import common.NEWLINE
%import common.INT

%ignore WS_INLINE
%ignore COMMENT
